apply plugin: 'java'

sourceCompatibility = 1.7
targetCompatibility = 1.7

defaultTasks = ['test', 'packageAll']

def srcDir = 'src'
def webDir = 'web'
def testDir = 'test'
def warDir = 'build/war'
def extraDir = 'build/gwt-extras'
def gwtModuleName = 'fr.mncc.sandbox.sandbox'

sourceSets {
    main {
        java {
            srcDirs = [srcDir]
        }
        output.classesDir = (warDir + '/WEB-INF/classes')
    }
	unitTest {
        java {
			srcDirs = [testDir]
		}
		output.classesDir = (warDir + '/WEB-INF/classes_tests')
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // GWT START
	compile group: 'com.google.gwt', name: 'gwt-user', version: '2.7.0'
    compile group: 'com.google.gwt', name: 'gwt-dev', version: '2.7.0'
    compile group: 'com.google.gwt', name: 'gwt-elemental', version: '2.7.0'
    compile group: 'com.google.gwt', name: 'gwt-servlet', version: '2.7.0'
	// GWT END
	
	// APPENGINE START
	compile group: 'com.google.appengine', name: 'appengine-api-1.0-sdk', version: '1.9.18'	// Compile / Runtime
	compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'	// Compile / Runtime
	compile group: 'jstl', name: 'jstl', version: '1.2'	// Compile / Runtime
    compile group: 'org.mockito', name: 'mockito-all', version: '1.9.0'	// Test
	compile group: 'com.google.appengine', name: 'appengine-testing', version: '1.9.18'	// Test
	compile group: 'com.google.appengine', name: 'appengine-api-stubs', version: '1.9.18'	// Test
	// APPENGINE END
	
	// GWT-JSON-RPC START
	compile group: 'com.google.code.gson', name: 'gson', version: '2.1'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.4'
	// GWT-JSON-RPC END
	
	// GUAVA START
	compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'com.google.guava', name: 'guava-gwt', version: '18.0'
	// GUAVA END
	
	// Compile java for tests
    unitTestCompile 'junit:junit:4.11'
	unitTestCompile files(warDir + '/WEB-INF/classes')
	
    // Runtime GWT libraries, should be included in WAR
    compile fileTree(dir: webDir + '/WEB-INF/libs', include: '*.jar')
}

configurations {
    unitTestCompile.extendsFrom runtime
    unitTestRuntime.extendsFrom unitTestCompile
}

task unitTests(dependsOn: 'testClasses', type:Test) {
	
	testLogging {
		events 'started', 'passed'
	}
	
    testClassesDir = sourceSets.unitTest.output.classesDir
    classpath = sourceSets.unitTest.runtimeClasspath
}

task executeGwtCompiler(dependsOn: ['unitTests', 'classes'], type: JavaExec) {

    inputs.source sourceSets.main.java.srcDirs
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir warDir

    main = 'com.google.gwt.dev.Compiler'

    classpath {
        [
			webDir + '/WEB-INF/libs',               // Misc. libraries
			sourceSets.main.java.srcDirs,           // Java sources
			sourceSets.main.output.resourcesDir,    // Generated resources
			sourceSets.main.output.classesDir,      // Generated classes
			sourceSets.main.compileClasspath,       // Misc. dependencies
        ]
    }
	
    args gwtModuleName,
            '-war', warDir,
            '-extra', extraDir,
            '-localWorkers', '2',
            '-style', 'OBFUSCATED',
            '-strict',
            '-XdisableCastChecking',
            '-XdisableClassMetadata',
            '-XenableClosureCompiler'
}

task packageAll(dependsOn: ['executeGwtCompiler']) {
    copy {
        from webDir
        into warDir
    }
    copy {
        from sourceSets.main.output.classesDir
        into(warDir + '/WEB-INF/libs')
    }
    copy {
        from configurations.runtime
        into(warDir + '/WEB-INF/libs')
    }
}
